!
!///////////////////////////////////////////////////////////////////////////////////////////////////////
!
!    HORSES2D - A high-order discontinuous Galerkin spectral element solver.
!    Copyright (C) 2017  Juan Manzanero Torrico (juan.manzanero@upm.es)
!
!    This program is free software: you can redistribute it and/or modify
!    it under the terms of the GNU General Public License as published by
!    the Free Software Foundation, either version 3 of the License, or
!    (at your option) any later version.
!
!    This program is distributed in the hope that it will be useful,
!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!    GNU General Public License for more details.
!
!    You should have received a copy of the GNU General Public License
!    along with this program.  If not, see <http://www.gnu.org/licenses/>.
!
!////////////////////////////////////////////////////////////////////////////////////////////////////////
!
!
!        **********************************************************************************
!                 Auxiliar subroutines
!        **********************************************************************************
!
         subroutine searchEdge( nodesEl , nodesEdge , needsInversion , edgePosition , edgeDirection )
!
!           ******************************************************************************************
!                 Given two nodes, get the edge ID that belongs and its direction
!                    -> nodesEL: four nodes ID of the element
!                    -> nodesEdge: two nodes ID of an edge
!                    -> edgePosition: position of the edge in the element: TOP, LEFT, RIGHT, BOTTOM
!                    -> edgeDirection: linking edge/element: FORWARD, BACKWARD
!           ******************************************************************************************
            implicit none
            integer,         intent (in)        :: nodesEl(POINTS_PER_QUAD)
            integer,         intent (in)        :: nodesEdge(POINTS_PER_EDGE)
            logical,         intent (out)       :: needsInversion
            integer(kind=1), intent (out)       :: edgePosition
            integer(kind=1), intent (out)       :: edgeDirection
!           -----------------------------------------------------------
            integer                    :: currentEdge(POINTS_PER_EDGE)
            integer(kind=1)            :: edge
            logical                    :: edges_are_equal

            do edge = 1 , EDGES_PER_QUAD
!
!              Obtain current edge of the element
!              ----------------------------------
               currentEdge(1) = nodesEl(edge)
               if (edge .eq. EDGES_PER_QUAD) then
                  currentEdge(2) = nodesEl(1)
               else
                  currentEdge(2) = nodesEl(edge+1)
               end if
!
!              Compare the element edge with the original edge
!              -----------------------------------------------
               call compareEdges( edge1 = currentEdge , edge2 = nodesEdge , edge_position = edge , edges_are_equal = edges_are_equal , edgeDirection = edgeDirection )

               if (edges_are_equal) then
                  edgePosition = edge
                  if ( edgeDirection .eq. FORWARD ) then
                     needsInversion = .false.
                  else
                     needsInversion = .true.
                  end if

                  return

               end if

            end do   
!
!           If not found
!           ------------
            edgePosition = -1
            edgeDirection = 0
            needsInversion = .false.

         end subroutine searchEdge

         subroutine compareEdges ( edge1 , edge2 , edge_position , edges_are_equal , edgeDirection )
            implicit none
            integer,         intent (in)        :: edge1(:)
            integer,         intent (in)        :: edge2(:)
            integer(kind=1), intent (in)        :: edge_position
            logical,         intent (out)       :: edges_are_equal
            integer(kind=1), intent (out)       :: edgeDirection

            if ((edge1(1) .eq. edge2(1)) .and. (edge1(2) .eq. edge2(2))) then
               edges_are_equal = .true.
               if ( (edge_position .eq. EBOTTOM) .or. (edge_position .eq. ERIGHT) ) then
                  edgeDirection = FORWARD
                  
               else
                  edgeDirection = BACKWARD

               end if
            
            elseif ((edge1(1) .eq. edge2(2)) .and. (edge1(2) .eq. edge2(1))) then
               edges_are_equal = .true.
               if ( (edge_position .eq. EBOTTOM) .or. (edge_position .eq. ERIGHT) ) then
                  edgeDirection = BACKWARD
                  
               else
                  edgeDirection = FORWARD

               end if
 
            else
               edges_are_equal = .false.
               edgeDirection = 0
            end if

         end subroutine compareEdges
