!
!///////////////////////////////////////////////////////////////////////////////////
!
!              RIEMANN BOUNDARY CONDITION
!              --------------------------
!///////////////////////////////////////////////////////////////////////////////////
!
      subroutine RiemannBC_Construct( self , marker , in_label)
         use Setup_class
         implicit none
         class(RiemannBC_t)         :: self
         integer                    :: marker
         character(len=*)           :: in_label
         real(kind=RP), allocatable :: pressure
         real(kind=RP), allocatable :: Temperature
         real(kind=RP), allocatable :: Mach
         real(kind=RP), allocatable :: AngleOfAttack
         real(kind=RP)              :: rho
         character(len=STR_LEN_BC)  :: mode

         call readValueInRegion( trim(Setup % bdry_file) , "pressure" , pressure , in_label , "# end")
         call readValueInRegion( trim(Setup % bdry_file) , "Temperature", Temperature , in_label , "# end")
         call readValueInRegion( trim(Setup % bdry_file) , "Mach" , Mach , in_label , "# end")
         call readValueInRegion( trim(Setup % bdry_file) , "Angle of attack" , AngleOfAttack , in_label , "# end")
         call readValueInRegion( trim(Setup % bdry_file) , "Mode" , mode , in_label , "# end")
         
         if ( allocated(pressure) ) then
            pressure = pressure / refValues % p
         else
            allocate(pressure)
            pressure = 1.0_RP
         end if

         if ( allocated(Temperature) ) then
            Temperature = Temperature / refValues % T
         else
            allocate(Temperature)
            Temperature = 1.0_RP
         end if

         if ( .not. allocated(Mach) ) then
            allocate(Mach)
            Mach = Dimensionless % Mach
         end if

         if ( allocated(AngleOfAttack) ) then
            AngleOfAttack = AngleOfAttack * PI / 180.0_RP
         else
            allocate(AngleOfAttack)
            AngleOfAttack = 0.0_RP
         end if

         if ( trim(mode) .eq. "Velocity inlet" ) then
            self % mode = SPECIFY_SPEED
         elseif ( trim(mode) .eq. "Total pressure" ) then
            self % mode = SPECIFY_TOTAL_PRESSURE
         else        ! Default mode
            self % mode = SPECIFY_SPEED
         end if
!   
!        Construct the state vector
!        --------------------------
         associate ( gamma => Thermodynamics % Gamma , cv => Dimensionless % cv , gm1 => Thermodynamics % gm1 , gogm1 => Thermodynamics % gogm1 )

         rho = pressure / Temperature

         self % AngleOfAttack = AngleOfAttack
         self % Tt            = Temperature * ( 1.0_RP + 0.5_RP * gm1 * Mach * Mach)
         self % pt            = pressure * ( self % Tt / Temperature ) ** Dimensionless % cp
         self % Ht            = Temperature * Dimensionless % cp + 0.5_RP * gamma * Mach * Mach

         if ( self % mode .eq. SPECIFY_SPEED ) then
            self % q(IRHO)  = rho
            self % q(IRHOU) = rho * sqrt(gamma) * Mach * cos(AngleOfAttack)
            self % q(IRHOV) = rho * sqrt(gamma) * Mach * sin(AngleOfAttack)
            self % q(IRHOE) = cv * pressure + 0.5_RP * rho * gamma * Mach * Mach

            self % w(IRHO)  = rho
            self % w(IU) = dimensionless % sqrtGammaMach * cos(AngleOfAttack)
            self % w(IV) = dimensionless % sqrtGammaMach * sin(AngleOfAttack)
            self % w(IP) = pressure
            self % w(IT) = pressure / rho
            self % w(IA) = sqrt(gamma * self % w(IT))

         elseif ( self % mode .eq. SPECIFY_TOTAL_PRESSURE ) then
            self % q(IRHO)  = self % pt / self % Tt
            self % q(IRHOU) = 0.0_RP
            self % q(IRHOV) = 0.0_RP
            self % q(IRHOE) = cv * self % pt

            self % w(IRHO)  = rho
            self % w(IU) = 0.0_RP
            self % w(IV) = 0.0_RP
            self % w(IP) = pressure
            self % w(IT) = pressure / rho
            self % w(IA) = sqrt(gamma * self % w(IT))
         else
            print*, "Unknown mode"
            stop "Stopped."
         end if

         end associate

      end subroutine RiemannBC_Construct

      subroutine RiemannBC_Associate(self , edge)
!
!        *****************************************************
!           This is a weakly-prescribed boundary condition.
!              Thus, self % FB, the fluxes, is allocated.
!        *****************************************************
!
         implicit none
         class(RiemannBC_t) :: self
         class(Edge_t)      :: edge
         integer            :: i

         associate ( N => edge % spA % N )

         select type ( edge )
         
            type is (Edge_t)
               print*, "Only boundary edges are expected."
               stop "Stopped"
      
            type is (StraightBdryEdge_t)
               allocate ( edge % FB(0:N,NCONS) ) 
               allocate ( edge % uB(0:N,NCONS) ) 

               edge % BCWeakType = self % WeakType

               edge % RiemannSolver => self % RiemannSolver

               edge % wB(0:,1:)    => edge % storage(1) % W(0:,1:)
#ifdef NAVIER_STOKES
               edge % gB(0:,1:,1:) => edge % storage(1) % dQ(0:,1:,1:)
#endif
   
            type is (CurvedBdryEdge_t)
               allocate ( edge % FB(0:N,NCONS) ) 
               allocate ( edge % uB(0:N,NCONS) ) 

               edge % BCWeakType = self % WeakType

               edge % RiemannSolver => self % RiemannSolver

               edge % wB(0:,1:)    => edge % storage(1) % W(0:,1:)
#ifdef NAVIER_STOKES
               edge % gB(0:,1:,1:) => edge % storage(1) % dQ(0:,1:,1:)
#endif

         end select
         end associate

      end subroutine RiemannBC_Associate

      subroutine RiemannBC_UpdateSolution( self , edge )
         implicit none
         class(RiemannBC_t)          :: self
         class(Edge_t)                       :: edge
         integer                       :: iXi
         integer                    :: N 
         real(kind=RP)              :: rhoL , vnL , uL , vL , pL , ML , aL  , TL , Rplus
         real(kind=RP)              :: rhoR , vnR , uR , vR , pR , VtR , TR , aR , MR , sR
         real(kind=RP)              :: rhoInfty, uInfty, vInfty , pInfty  , vnInfty , aInfty
         real(kind=RP)              :: nInfty(NDIM)
         real(kind=RP)              :: F(NCONS , NDIM)
         real(kind=RP)              :: q(NCONS)
         real(kind=RP)              :: Rminus
!

         associate ( gamma => Thermodynamics % gamma , gm1 => Thermodynamics % gm1 , cp => Dimensionless % cp , cv => Dimensionless % cv)

         rhoInfty = self % q(IRHO)
         uInfty   = self % w(IU)
         vInfty   = self % w(IV)
         pInfty   = self % w(IP)
         aInfty   = self % w(IA)
         nInfty   = [cos(self % AngleOfAttack) , sin(self % AngleOfAttack)]
         

         N = edge % spA % N

         select type ( edge )
            type is (StraightBdryEdge_t) 
               do iXi = 0 , N
!
!                 First stage: Determine the boundary flow character
!                 --------------------------------------------------
                  rhoL = edge % storage(1) % W(iXi , IRHO)
                  uL  = edge % storage(1) % W(iXi , IU) 
                  vL  = edge % storage(1) % W(iXi , IV) 
                  vnL = uL * edge % n (IX , iXi) + vL * edge % n(IY, iXi)
                  pL  = edge % storage(1) % W(iXi , IP)
                  aL  = edge % storage(1) % W(iXi , IA)
                  TL  = edge % storage(1) % W(iXi , IT)
                  ML  = vnL / aL

                  vnInfty = uInfty * edge % n (IX , iXi) + vInfty * edge % n(IY, iXi)
!
!                 Second stage: Compute the "Right" state depending on the result
!                 ---------------------------------------------------------------
                  if ( ML .lt. -1.0_RP ) then      ! Supersonic inflow
                     q = self % q
                     F = inviscidFlux( q )
                     edge % FB(iXi,1:NCONS) = F(1:NCONS,IX) * edge % dS(IX,iXi) + F(1:NCONS , IY) * edge % dS(IY,iXi)

                     edge % uB(iXi,1:NCONS) = q
                     
                  elseif ( ML .lt. 0.0_RP ) then   ! Subsonic inflow

                     Rminus = vnInfty - 2.0_RP * cv * aInfty
                     Rplus = vnL + 2.0_RP * cv * aL

                     vnR = 0.5_RP * (Rplus + Rminus)
                     aR  = 0.25_RP * gm1 * (Rplus - Rminus)

                     uR  = uInfty + (vnR - vninfty) * edge % n(IX , iXi)
                     vR  = vInfty + (vnR - vninfty) * edge % n(IY , iXi)

                     sR   = (aInfty*aInfty) * (rhoInfty ** (-gm1) )  / gamma
                     rhoR = (aR * aR / ( gamma * sR ) ) ** (cv)
                     pR   = rhoR * aR * aR / gamma
                      
                     q(IRHO) = rhoR
                     q(IRHOU) = rhoR * uR
                     q(IRHOV) = rhoR * vR
                     q(IRHOE) = cv * pR + 0.5_RP * ( q(IRHOU) * uR + q(IRHOV) * vR )

                     F = inviscidFlux( q )
                     edge % FB(iXi,1:NCONS) = F(1:NCONS,IX) * edge % dS(IX,iXi) + F(1:NCONS , IY) * edge % dS(IY,iXi)

                     edge % uB(iXi,1:NCONS) = q
                     
                  elseif ( ML .le. 1.0_RP ) then   ! Subsonic outflow

                     Rminus = vnInfty - 2.0_RP * cv * aInfty
                     Rplus = vnL + 2.0_RP * cv * aL

                     vnR = 0.5_RP * (Rplus + Rminus)
                     aR  = 0.25_RP * gm1 * (Rplus - Rminus)
   
                     uR = uL + (vnR - vnL) * edge % n(IX,iXi)
                     vR = vL + (vnR - vnL) * edge % n(IY,iXi)

                     sR   = (aL*aL) * (rhoL ** (-gm1) )  / gamma
                     rhoR = (aR * aR / ( gamma * sR )) ** (cv)
                     pR   = rhoR * aR * aR / gamma

                     q(IRHO) = rhoR
                     q(IRHOU) = rhoR * uR 
                     q(IRHOV) = rhoR * vR 
                     q(IRHOE) = cv * pR + 0.5_RP * ( q(IRHOU) * uR + q(IRHOV) * vR )

                     F = inviscidFlux( q )
                     edge % FB(iXi,1:NCONS) = F(1:NCONS,IX) * edge % dS(IX,iXi) + F(1:NCONS , IY) * edge % dS(IY,iXi)

                     edge % uB(iXi,1:NCONS) = q
                     
                  elseif ( ML .ge. 1.0_RP ) then   ! Supersonic outflow
                     q = edge % storage(1) % Q(iXi,1:NCONS)
                     F = inviscidFlux( q )
                     edge % FB(iXi,1:NCONS) = F(1:NCONS,IX) * edge % dS(IX,iXi) + F(1:NCONS , IY) * edge % dS(IY,iXi)

                     edge % uB(iXi,1:NCONS) = q
                     
                  end if
      
               end do 

            type is (CurvedBdryEdge_t) 

               do iXi = 0 , N
!
!                 First stage: Determine the boundary flow character
!                 --------------------------------------------------
                  rhoL = edge % storage(1) % W(iXi , IRHO)
                  uL  = edge % storage(1) % W(iXi , IU) 
                  vL  = edge % storage(1) % W(iXi , IV) 
                  vnL = uL * edge % n (IX , iXi) + vL * edge % n(IY, iXi)
                  pL  = edge % storage(1) % W(iXi , IP)
                  aL  = edge % storage(1) % W(iXi , IA)
                  TL  = edge % storage(1) % W(iXi , IT)
                  ML  = vnL / aL

                  vnInfty = uInfty * edge % n (IX , iXi) + vInfty * edge % n(IY, iXi)
!
!                 Second stage: Compute the "Right" state depending on the result
!                 ---------------------------------------------------------------
                  if ( ML .lt. -1.0_RP ) then      ! Supersonic inflow
                     q = self % q
                     F = inviscidFlux( q )
                     edge % FB(iXi,1:NCONS) = F(1:NCONS,IX) * edge % dS(IX,iXi) + F(1:NCONS , IY) * edge % dS(IY,iXi)

                     edge % uB(iXi,1:NCONS) = q
                     
                  elseif ( ML .lt. 0.0_RP ) then   ! Subsonic inflow

                     Rminus = vnInfty - 2.0_RP * cv * aInfty
                     Rplus = vnL + 2.0_RP * cv * aL

                     vnR = 0.5_RP * (Rplus + Rminus)
                     aR  = 0.25_RP * gm1 * (Rplus - Rminus)

                     uR  = uInfty + (vnR - vninfty) * edge % n(IX , iXi)
                     vR  = vInfty + (vnR - vninfty) * edge % n(IY , iXi)

                     sR   = (aInfty*aInfty) * (rhoInfty ** (-gm1) )  / gamma
                     rhoR = (aR * aR / ( gamma * sR ) ) ** (cv)
                     pR   = rhoR * aR * aR / gamma
                      
                     q(IRHO) = rhoR
                     q(IRHOU) = rhoR * uR
                     q(IRHOV) = rhoR * vR
                     q(IRHOE) = cv * pR + 0.5_RP * ( q(IRHOU) * uR + q(IRHOV) * vR )

                     F = inviscidFlux( q )
                     edge % FB(iXi,1:NCONS) = F(1:NCONS,IX) * edge % dS(IX,iXi) + F(1:NCONS , IY) * edge % dS(IY,iXi)

                     edge % uB(iXi,1:NCONS) = q
                     
                  elseif ( ML .le. 1.0_RP ) then   ! Subsonic outflow

                     Rminus = vnInfty - 2.0_RP * cv * aInfty
                     Rplus = vnL + 2.0_RP * cv * aL

                     vnR = 0.5_RP * (Rplus + Rminus)
                     aR  = 0.25_RP * gm1 * (Rplus - Rminus)
   
                     uR = uL + (vnR - vnL) * edge % n(IX,iXi)
                     vR = vL + (vnR - vnL) * edge % n(IY,iXi)

                     sR   = (aL*aL) * (rhoL ** (-gm1) )  / gamma
                     rhoR = (aR * aR / ( gamma * sR )) ** (cv)
                     pR   = rhoR * aR * aR / gamma

                     q(IRHO) = rhoR
                     q(IRHOU) = rhoR * uR 
                     q(IRHOV) = rhoR * vR 
                     q(IRHOE) = cv * pR + 0.5_RP * ( q(IRHOU) * uR + q(IRHOV) * vR )

                     F = inviscidFlux( q )
                     edge % FB(iXi,1:NCONS) = F(1:NCONS,IX) * edge % dS(IX,iXi) + F(1:NCONS , IY) * edge % dS(IY,iXi)

                     edge % uB(iXi,1:NCONS) = q
                     
                  elseif ( ML .ge. 1.0_RP ) then   ! Supersonic outflow
                     q = edge % storage(1) % Q(iXi,1:NCONS)
                     F = inviscidFlux( q )
                     edge % FB(iXi,1:NCONS) = F(1:NCONS,IX) * edge % dS(IX,iXi) + F(1:NCONS , IY) * edge % dS(IY,iXi)

                     edge % uB(iXi,1:NCONS) = q
                     
                  end if
      
               end do 

            class default
         end select

         end associate

      end subroutine RiemannBC_UpdateSolution
